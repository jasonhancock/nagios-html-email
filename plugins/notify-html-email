#!/usr/bin/perl

# Copyright (c) 2012 Jason Hancock <jsnbyh@gmail.com>
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is furnished
# to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in all
# copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
# THE SOFTWARE.
#
# This file is part of the nagios-cpu bundle that can be found
# at https://github.com/jasonhancock/nagios-html-email

use strict;
use warnings;

use MIME::Lite;
use URI::Escape;

my $nagios_url = $ARGV[0] || die('You must pass the url to your nagios installation as the first argument');

my %allowed_types = (
    'PROBLEM'         => 1,
    'RECOVERY'        => 1,
    'ACKNOWLEDGEMENT' => 1
);

exit if !defined($allowed_types{$ENV{NAGIOS_NOTIFICATIONTYPE}});
my $type = $ENV{NAGIOS_SERVICEATTEMPT} ? 'service' : 'host';

my $subject = sprintf('** %s %s Alert: %s is %s **',
    $ENV{NAGIOS_NOTIFICATIONTYPE},
    ucfirst($type),
    $type eq 'service' ? $ENV{NAGIOS_HOSTNAME} . '/' . $ENV{NAGIOS_SERVICEDESC} : $ENV{NAGIOS_HOSTNAME},
    $type eq 'service' ? $ENV{NAGIOS_SERVICESTATE} : $ENV{NAGIOS_HOSTSTATE}
);

my $host_url = sprintf('%s/cgi-bin/status.cgi?navbarsearch=1&host=%s',
    $nagios_url,
    $ENV{NAGIOS_HOSTNAME}
);
my @rows;
my $notification_title;

if($type eq 'service') {
    my $service_url = sprintf('%s/cgi-bin/extinfo.cgi?type=2&host=%s&service=%s',
        $nagios_url,
        $ENV{NAGIOS_HOSTNAME},
        uri_escape($ENV{NAGIOS_SERVICEDESC})
    );

    $notification_title = 'Nagios Service Notification';

    @rows = (
        {
            'title' => 'Service State:',
            'data'  => $ENV{NAGIOS_SERVICESTATE},
            'class' => lc($ENV{NAGIOS_SERVICESTATE}),
        },
        {
            'title' => 'Notification Type:',
            'data'  => $ENV{NAGIOS_NOTIFICATIONTYPE},
        },
        {
            'title' => 'Hostname:',
            'data'  => "<a href=\"$host_url\">$ENV{NAGIOS_HOSTNAME}</a>",
        },
        {
            'title' => 'Service Name:',
            'data'  => "<a href=\"$service_url\">$ENV{NAGIOS_SERVICEDESC}</a>",
        },
        {
            'title' => 'Service Data:',
            'data'  => $ENV{NAGIOS_SERVICEOUTPUT},
        },
        {
            'title' => 'IP Address:',
            'data'  => $ENV{NAGIOS_HOSTADDRESS},
        },
        {
            'title' => 'Hostgroups:',
            'data'  => $ENV{NAGIOS_HOSTGROUPNAME},
        },
        {
            'title' => 'Event Time:',
            'data'  => $ENV{NAGIOS_SHORTDATETIME},
        },
    );
} else {
    $notification_title = 'Nagios Host Notification';

    @rows = (
        {
            'title' => 'Host State:',
            'data'  => $ENV{NAGIOS_HOSTSTATE},
            'class' => lc($ENV{NAGIOS_HOSTSTATE}),
        },
        {
            'title' => 'Notification Type:',
            'data'  => $ENV{NAGIOS_NOTIFICATIONTYPE},
        },
        {
            'title' => 'Hostname:',
            'data'  => "<a href=\"$host_url\">$ENV{NAGIOS_HOSTNAME}</a>",
        },
        {
            'title' => 'Host Data:',
            'data'  => $ENV{NAGIOS_HOSTOUTPUT},
        },
        {
            'title' => 'IP Address:',
            'data'  => $ENV{NAGIOS_HOSTADDRESS},
        },
        {
            'title' => 'Hostgroups:',
            'data'  => $ENV{NAGIOS_HOSTGROUPNAMES},
        },
        {
            'title' => 'Event Time:',
            'data'  => $ENV{NAGIOS_SHORTDATETIME},
        },
    );
}

if($ENV{NAGIOS_NOTIFICATIONTYPE} eq 'ACKNOWLEDGEMENT') {
    push(@rows,
        {
            'title' => 'Acknowledged By:',
            'data'  => $ENV{NAGIOS_NOTIFICATIONAUTHOR},
        }
    );
    push(@rows,
        {
            'title' => 'Comment:',
            'data'  => $ENV{NAGIOS_NOTIFICATIONCOMMENT},
        }
    );
}

# Begin the email
my $font = 'helvetica, arial';

my %colors= (
    red    => '#FF8080',
    green  => '#80FF80',
    yellow => '#FFFF80',
    orange => '#FF9900',
    even   => '#C9C9C9',
    odd    => '#EAEAEA',
);

my $body = <<"EOF";
<head>
    <style>
h3 {
    font-family: $font;
    color: black;
}
table {
    font-family: $font;
    font-size: 12px;
    border: 0;
    cellpadding: 4;
    cellspacing: 2;
}
th.odd {
    background-color: $colors{odd};
    font-weight: bold;
    width: 130px;
}
th.even {
    background-color: $colors{even};
    font-weight: bold;
    width: 130px;
}
td.odd {
    background-color: $colors{odd};
}
td.even {
    background-color: $colors{even};
}
td.problem {
    background-color: $colors{red};
}
td.recovery {
    background-color: $colors{green};
}
td.acknowledgement {
    background-color: $colors{yellow};
}
td.critical {
    background-color: $colors{red};
}
td.warning {
    background-color: $colors{yellow};
}
td.ok {
    background-color: $colors{green};
}
td.unknown {
    background-color: $colors{orange};
}
td.up {
    background-color: $colors{green};
}
td.down {
    background-color: $colors{red};
}
td.unreachable {
    background-color: $colors{orange};
}
    </style>
</head>
<body>
    <h3>$notification_title</h3>
    <table>
EOF

# Add the rows to the email
for(my $i=0; $i<@rows; $i++) {
    my $th_class = $i % 2 ? 'odd' : 'even';
    my $td_class = $rows[$i]->{'class'} ? $rows[$i]->{'class'} : $th_class;
    my $title = $rows[$i]->{'title'};
    my $data = $rows[$i]->{'data'};
    die $title if !$data;
    $body .= <<"EOF";
        <tr>
            <th class="$th_class">$title</th>
            <td class="$td_class">$data</td>
        </tr>
EOF
}

$body .= <<"EOF";
    </table>
</body>
EOF

my $msg = MIME::Lite->new(
    To      => $ENV{NAGIOS_CONTACTEMAIL},
    Subject => $subject,
    Type    =>'multipart/related'
);

$msg->attach(Type => 'text/html', Data => $body);
$msg->send();
